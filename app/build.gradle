apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'kotlinx-serialization'

apply plugin: 'androidx.navigation.safeargs.kotlin'

// Jacoco support suggested by Rafael Toledo in a Medium post:
// https://proandroiddev.com/unified-code-coverage-for-android-revisited-44789c9b722f
apply plugin: 'jacoco'

jacoco {
    toolVersion = "$jacoco_version"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', 'androidx/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/app_classes/debug/bundleDebugClasses/classes.jar", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])
    executionData.from = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.pajato.argus.store"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    jacoco {
        // Suggested by Carmen Alvarez in a Medium post:
        // https://medium.com/@ms.carmen.alvarez/this-worked-for-me-up-until-upgrading-to-the-android-gradle-plugin-3-1-0-65b67763809
        version "$jacoco_version"
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }

    androidExtensions {
        experimental = true
    }

    packagingOptions {
        // Suggested by "hotkey" on Stack Overflow:
        // https://stackoverflow.com/questions/45232350/disable-meta-inf-generation-in-gradle-android-library-kotlin-project
        exclude 'META-INF/argus-file-store.kotlin_module'
        exclude 'META-INF/KFile.kotlin_module'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.0.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.pajato.argus:argus-file-store-jvm:0.0.1'
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"

    // The following should be available via transitive closure but is not for an unknown reason.
    implementation 'com.pajato.argus:argus-tmdb-core-jvm:0.0.12'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'

    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.0.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.pajato.io:KFile-jvm:0.0.2'
    androidTestImplementation "com.pajato.argus:argus-tmdb-client-jvm:0.1.0"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:1.3.31"
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'androidx.arch.core' && !details.requested.name.contains('core-testing')) {
            details.useVersion "2.0.0"
        }
    }
}
